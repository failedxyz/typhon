use std::str::FromStr;

use ast;
use common::{Literal, Type};

grammar;

pub Program: ast::Program = <decls:TopDecl+> => ast::Program(decls);

TopDecl: ast::TopDecl = {
    "extern" <name:r"[A-Za-z_][A-Za-z0-9_]*"> ":" <ty:Type> => ast::TopDecl::Extern(name.to_owned(), ty),
    "fn" <name:r"[A-Za-z_][A-Za-z0-9_]*"> "(" ")" ":" <ty:Type> "=" <stmts:Body> "end" => ast::TopDecl::Fn(name.to_owned(), ty, stmts),
};

Type: Type = {
    "int" => Type::Int,
};

Body: Vec<ast::Stmt> = {
    <f:Stmt?> <stmts:(";" <Stmt>)*> ";"? => match f {
        None => stmts,
        Some(s) => {
            let mut stmts = stmts;
            stmts.insert(0, s);
            stmts
        },
    }
};

Stmt: ast::Stmt = {
    <name:r"[A-Za-z_][A-Za-z0-9_]*"> "=" <expr:Expr> => ast::Stmt::Assign(name.to_owned(), expr),
    <expr:Expr> => ast::Stmt::Expr(expr),
    "if" <cond:Expr> ":" <body1:Body> <body2:("else" ":" Body)?> "end" => ast::Stmt::If(cond, body1, body2.map(|(_, _, body)| body)),
    "return" <expr:Expr?> => ast::Stmt::Return(expr),
};

Expr: ast::Expr = {
    <left:Expr> "+" <right:Term> => ast::Expr::Plus(Box::new(left), Box::new(right)),
    <left:Expr> "-" <right:Term> => ast::Expr::Minus(Box::new(left), Box::new(right)),
    <right:Term> => right,
};

Term: ast::Expr = {
    <name:r"[A-Za-z_][A-Za-z0-9_]*"> "(" ")" => ast::Expr::Call(name.to_owned(), Vec::new()),
    <literal:Literal> => ast::Expr::Literal(literal),
    <name:r"[A-Za-z_][A-Za-z0-9_]*"> => ast::Expr::Name(name.to_owned()),
};

Literal: Literal = {
    <int:r"[0-9]+"> => Literal::Int(u32::from_str(int).unwrap()),
};
