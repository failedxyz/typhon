use std::str::FromStr;

use ast;
use common::{Literal, Arg, Type};

grammar;

pub Program: ast::Program = <decls:TopDecl+> => ast::Program(decls);

TopDecl: ast::TopDecl = {
    "extern" <name:r"[A-Za-z_][A-Za-z0-9_]*"> ":" <ty:Type> => ast::TopDecl::Extern(name.to_owned(), ty),
    "fn" <name:r"[A-Za-z_][A-Za-z0-9_]*"> "(" <args:Comma<Arg>> ")" ":" <ty:Type> "=" <stmts:Body> "end" => ast::TopDecl::Fn(name.to_owned(), args, ty, stmts),
};

Type: Type = {
    "int" => Type::Int,
};

Body: Vec<ast::Stmt> = {
    <f:Stmt?> <stmts:(";" <Stmt>)*> ";"? => match f {
        None => stmts,
        Some(s) => {
            let mut stmts = stmts;
            stmts.insert(0, s);
            stmts
        },
    }
};

Stmt: ast::Stmt = {
    <name:r"[A-Za-z_][A-Za-z0-9_]*"> "=" <expr:Expr> => ast::Stmt::Assign(name.to_owned(), expr),
    <expr:Expr> => ast::Stmt::Expr(expr),
    "if" <cond:Expr> ":" <body1:Body> <body2:("else" ":" Body)?> "end" => ast::Stmt::If(cond, body1, body2.map(|(_, _, body)| body)),
    "return" <expr:Expr?> => ast::Stmt::Return(expr),
};

Expr: ast::Expr = CompExpr;

CompExpr: ast::Expr = {
    <left:CompExpr> "==" <right:ArithExpr> => ast::Expr::Equals(Box::new(left), Box::new(right)),
    <expr:ArithExpr> => expr,
};

ArithExpr: ast::Expr = {
    <left:ArithExpr> "+" <right:FactorExpr> => ast::Expr::Plus(Box::new(left), Box::new(right)),
    <left:ArithExpr> "-" <right:FactorExpr> => ast::Expr::Minus(Box::new(left), Box::new(right)),
    <expr:FactorExpr> => expr,
};

FactorExpr: ast::Expr = {
    <left:FactorExpr> "*" <right:Term> => ast::Expr::Times(Box::new(left), Box::new(right)),
    <expr:Term> => expr,
};

Term: ast::Expr = ExprBottom;

ExprBottom: ast::Expr = {
    <name:r"[A-Za-z_][A-Za-z0-9_]*"> "(" <args:Comma<Expr>> ")" => ast::Expr::Call(name.to_owned(), args),
    <literal:Literal> => ast::Expr::Literal(literal),
    <name:r"[A-Za-z_][A-Za-z0-9_]*"> => ast::Expr::Name(name.to_owned()),
};

Literal: Literal = {
    <int:r"[0-9]+"> => Literal::Int(u32::from_str(int).unwrap()),
    <string:r#""[^"]*""#> => Literal::String(string[1..string.len() - 1].to_owned()),
};

Arg: Arg = {
    <name:r"[A-Za-z_][A-Za-z0-9_]*"> ":" <ty:Type> => Arg(name.to_owned(), ty),
};

Comma<T>: Vec<T> = {
    <f:T?> <v:("," <T>)*> ","? => match f {
        None => v,
        Some(s) => {
            let mut v = v;
            v.insert(0, s);
            v
        },
    }
};
